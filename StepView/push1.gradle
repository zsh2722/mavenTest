// 引入maven工具
apply plugin: 'maven'
// 生成Javadoc文档
task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
// 生成jar包
task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}
// 防止编码问题
tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}
artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            // url 私服仓库地址
            repository(url: "http://192.168.1.8:8081/repository/maven-3rd-party/") {
                // userName 私服仓库账号 password 私服仓库账号密码
                authentication(userName: "admin", password: "admin123")
            }

            pom.project {
                name 'step-view'//项目名称
                version '1.0.1'//版本号
                artifactId 'step-view'//最后下载的aar包名称就是这个
                groupId 'com.zsh'// 建议使用包命
                packaging 'aar'//打包类型
                description 'step-view for Android'// 描述信息
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }
}