// 第四种
apply plugin: 'maven' // 引入maven工具

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}
task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}
// 防止编码问题
tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "http://192.168.1.8:8081/repository/maven-3rd-party/") {//仓库的uri
                authentication(userName: "admin", password: "admin123")// 登入账号和密码
            }
            pom.project {
                name 'step-view'//项目名称
                version '1.0.3'//版本号
                artifactId 'step-view'//最后下载的aar包名称就是这个
                groupId 'com.zsh'// 建议使用包命
                packaging 'aar'//打包类型
                description 'step-view for Android'// 描述信息
            }
        }
    }
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}